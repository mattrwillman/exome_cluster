#Apptainer is used to access GATK version 4.6
#Highest GATK version available in conda build is currently 4.5
#GATK version 4.5 stores missing marker calls as 0/0, which may be incorrectly interpreted by some downstream software as homozygous ref
#GATK version 4.6 stores missing marker calls as -/-

import os

wildcards = glob_wildcards(config['FASTQ_DIR'] + "/{sample}_interleaved.fastq.gz")
SAMPLES = wildcards.sample

REF_GENOTYPE = config['REF_GENOTYPE']
REF_FASTA = config['REF_FASTA']
# REF_DICT = os.path.splitext(REF_FASTA)[0] + '.dict'
# REF_FAI = REF_FASTA + '.fai'
FASTQ_DIR = config['FASTQ_DIR']
PICARD_JAR = config['PICARD_JAR']
GATK_SIF = config['GATK_SIF']

rule all:
    input:
        expand("results/fastqc_out/raw/{sample}_interleaved_fastqc.html", sample = SAMPLES), 
        expand("results/fastqc_out/trimmed/{sample}.{r}.trimmed_fastqc.html", sample = SAMPLES, r = ["F", "R"]), 
        expand("results/samtools_stats/{sample}.txt", sample = SAMPLES), 
        expand("results/srww_exome_{interval}.vcf", interval = config['INTERVALS']), 
        "results/srww_exome_allChr.vcf", 
        "results/srww_exome_allChr.mm90.maf05.ld95.vcf"

rule fastqc_raw:
    input:
        expand("{dir}/{{sample}}_interleaved.fastq.gz", dir = FASTQ_DIR)
    output:
        "results/fastqc_out/raw/{sample}_interleaved_fastqc.html"
    params:
        outdir = "results/fastqc_out/raw"
    log:
        "logs/fastqc_raw/{sample}.log"
    shell:
        "fastqc {input} -o {params.outdir} 2> {log}"

rule fastp_trim:
    input:
        expand("{dir}/{{sample}}_interleaved.fastq.gz", dir = FASTQ_DIR)
    output:
        out1 = "results/data/trimmed/{sample}.F.trimmed.fastq",
        out2 = "results/data/trimmed/{sample}.R.trimmed.fastq" 
    log:
        "logs/fastp/{sample}.log"
    shell:
        "fastp --interleaved_in -i {input} "
        "--out1 {output.out1} --out2 {output.out2} 2> {log}"

rule fastqc_trimmed:
    input:
        "results/data/trimmed/{sample}.fastq"
    output:
        "results/fastqc_out/trimmed/{sample}_fastqc.html"
    params:
        outdir = "results/fastqc_out/trimmed"
    log:
        "logs/fastqc_trimmed/{sample}.log"
    shell:
        "fastqc {input} -o {params.outdir} 2> {log}"

rule hisat2_index:
    input:
        fasta = REF_FASTA
    output:
        expand("resources/idx/{ref}.{int}.ht2l", ref = REF_GENOTYPE, int = 1)
    params:
        ref = expand("resources/idx/{ref}", ref = REF_GENOTYPE)
    log:
        "logs/hisat2idx.log"
    threads: 12
    shell:
        "hisat2-build -p {threads} {input} {params.ref} &> {log}"

rule hisat2_align:
    input:
        in1 = "results/data/trimmed/{sample}.F.trimmed.fastq",
        in2 = "results/data/trimmed/{sample}.R.trimmed.fastq", 
        idx = expand("resources/idx/{ref}.{int}.ht2l", ref = REF_GENOTYPE, int = 1)
    output:
        sam = "results/alignment/{sample}.sam", 
        bam = "results/alignment/{sample}.bam", 
        sorted_bam = "results/alignment/{sample}.sorted.bam"
    params:
        ref = expand("resources/idx/{ref}", ref = REF_GENOTYPE)
    log:
        "logs/hisat2/{sample}.log"
    threads: 8
    shell:
        "hisat2 -x {params.ref} -p {threads} --rg-id {wildcards.sample} --rg SM:{wildcards.sample} "
        "-1 {input.in1} -2 {input.in2} > {output.sam} 2> {log} ; "
        "samtools view -Sb  -F 256 {output.sam} > {output.bam} 2>> {log} ; " #-F 256 removes non-primary alignments
        "samtools sort {output.bam} -o {output.sorted_bam} 2>> {log} ; "
        "samtools index -c {output.sorted_bam} 2>> {log}"

rule mark_dups:
    input:
        "results/alignment/{sample}.sorted.bam"
    output:
        bam = "results/alignment/{sample}.dedup.bam", 
        metrics = "logs/markdups/{sample}_metrics.txt"
    log:
        "logs/markdups/{sample}.log"
    shell:
        "gatk MarkDuplicates -I {input} -O {output.bam} -M {output.metrics} 2> {log} ; "
        "samtools index -c {output.bam} 2>> {log}"

rule samtools_stats:
    input:
        "results/alignment/{sample}.dedup.bam"
    output:
        "results/samtools_stats/{sample}.txt"
    log:
        "logs/samtools_stats/{sample}.log"
    threads: 8
    shell:
        "samtools stats {input} --threads {threads} > {output} 2> {log}"

rule create_sequence_dictionary:
    input:
        REF_FASTA
    output:
        expand("resources/idx/{ref}.dict", ref = REF_GENOTYPE)
#        REF_DICT
    params:
        picard = PICARD_JAR
    shell:
        "java -Xms5g -jar {params.picard} CreateSequenceDictionary -R {input} -O {output}"

rule create_fasta_index:
    input:
        ref = REF_FASTA
    output:
        fai = expand("resources/idx/{ref}.fai", ref = REF_GENOTYPE)
        #fai = REF_FAI
    shell:
        "samtools faidx {input}"

rule haplotypecaller:
    input:
        ref = REF_FASTA, 
        fai = expand("resources/idx/{ref}.fai", ref = REF_GENOTYPE), 
        #fai = REF_FAI, 
        dict = expand("resources/idx/{ref}.dict", ref = REF_GENOTYPE), 
#        dict = REF_DICT, 
        bam = "results/alignment/{sample}.dedup.bam"
    output:
        "results/calls/{sample}.g.vcf"
    log:
        "logs/haplotypecaller/{sample}.log"
    params:
        gatk_sif = GATK_SIF
    threads: 4
    shell:
        """
            apptainer exec {params.gatk_sif} gatk --java-options "-Xmx32g" HaplotypeCaller \
            -R {input.ref} \
            -I {input.bam} \
            -O {output} \
            -ERC GVCF \
            2> {log}
        """

rule makedatabase:
    input:
        gvcf = expand("results/calls/{sample}.g.vcf", sample = SAMPLES), 
        intervals = "resources/files/intervals.list"
    output:
        db = directory("results/db")
    log:
        "logs/gatk/genomicsdbimport.log"
    params:
        gatk_sif = GATK_SIF
    threads: 48
    run:
        flagged_gvcf = ["--variant " + f for f in input.gvcf]
        shell("""
            apptainer exec {params.gatk_sif} gatk --java-options "-Xmx1536g -Xms1500g" GenomicsDBImport {flagged_gvcf} \
            --genomicsdb-workspace-path {output.db} -L {input.intervals} 2> {log}
        """)

rule genotype:
    input:
        ref = REF_FASTA, 
        dict = expand("resources/idx/{ref}.dict", ref = REF_GENOTYPE), 
        db = "results/db"
    output:
        "results/srww_exome_{interval}.vcf"
    params:
        gatk_sif = GATK_SIF
    log:
        "logs/callgenotypes_{interval}.log"
    threads: 48
    shell:
        """
            apptainer exec {params.gatk_sif} gatk --java-options "-Xmx1536g -Xms1500g" GenotypeGVCFs \
            --sequence-dictionary {input.dict} \
            -R {input.ref} \
            -V gendb://{input.db} \
            -L {wildcards.interval} \
            -O {output} 2> {log}
        """
rule merge_vcfs:
    input:
        expand("results/srww_exome_{interval}.vcf", interval = config['INTERVALS'])
    output:
        "results/srww_exome_allChr.vcf"
    params:
        picard = PICARD_JAR
    log:
        "logs/merge_genotype_vcfs.log"
    run:
        vcfs = ["I=" + f for f in input]
        shell("""
            java -Xms1500g -jar {params.picard} MergeVcfs \
            {vcfs} \
            O={output} \
            2> {log}
        """)

rule filter_merged_vcf:
    input:
        "results/srww_exome_allChr.vcf"
    output:
        "results/srww_exome_allChr.mm90.maf05.vcf"
    params:
        maxmissing = "0.9", 
        maf = "0.05"
    log:
        "logs/filter_merged_vcf.log"
    shell:
        "vcftools --vcf {input} --max-missing {params.maxmissing} --maf {params.maf} --recode --stdout > {output} 2> {log}"

rule ld_thin:
    input:
        "results/srww_exome_allChr.mm90.maf05.vcf"
    output:
        "results/srww_exome_allChr.mm90.maf05.ld95.vcf"
    log:
        "logs/ld_thin_vcf.log"
    shell:
        "bcftools +prune -m 0.95 -w 1Mb {input} -Ov -o {output} 2> {log}"
